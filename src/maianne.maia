#!/usr/bin/env maiascript

///
/// @license
/// Copyright 2020 Roberto Luiz Souza Monteiro,
///                Renata Souza Barreto,
///                Hernane Borges de Barros Pereira.
///
/// Licensed under the Apache License, Version 2.0 (the "License")
/// you may not use this file except in compliance with the License.
/// You may obtain a copy of the License at
///
///   http://www.apache.org/licenses/LICENSE-2.0
///
/// Unless required by applicable law or agreed to in writing, software
/// distributed under the License is distributed on an "AS IS" BASIS,
/// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, eitherMath.express or implied.
/// See the License for the specific language governing permissions and
/// limitations under the License.
///

///
/// MaiaApp core class.
/// @namespace maianne
///
namespace maianne {
    appTitle = "MaiaNNE"
    appName = "maianne"
    scriptPath = "./"
    cnaLibrary = this.scriptPath + "/maia/cna/cna.maia"
    
    ///
    /// Calculates the average and the deviation of a time series.
    /// @method getAverage
    /// @memberof maianne
    /// @param {object}   data - The time series.
    /// @return {number}  Returns the average and the deviation of the time series.
    ///
    function getAverage(data) {
        average = 0
        deviation = 0
        minimum = data[0, 0]
        maximum = data[0, 0]

        sx = 0
        sx2 = 0
        n = data.length
                    
        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, 0]

            if (minimum > x) {
                minimum = x
            }

            if (maximum < x) {
                maximum = x
            }

            sx = sx + x
            sx2 = sx2 + x * x
        }

        average = sx / n
        deviation = math.sqrt((sx2 - (sx * sx) / n) / (n - 1))

        returnObject = {
            "average": average,
            "deviation": deviation,
            "minimum": minimum,
            "maximum": maximum,
            "n": n
        }

        return(returnObject)
    }

    ///
    /// Normalizes a time series.
    /// @method normalize
    /// @memberof maianne
    /// @param {object}   data - The time series.
    /// @param {number}   minimum - The minimum value of the time series.
    /// @param {number}   maximum - The maximum value of the time series.
    /// @return {number}  Returns the normalized time series.
    ///
    function normalize(data, minimum, maximum) {
        if ((core.type(minimum) == "undefined") || (core.type(maximum) == "undefined")) {
            avgData = this.getAverage(data)
            minimum = avgData.minimum
            maximum = avgData.maximum
        }

        normalizedData = []

        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, 0]
            if (core.type(x) == "number") {
                normalizedX = (x - minimum) / (maximum - minimum)
                normalizedData.push([normalizedX])
            }
        }

        return(normalizedData)
    }

    ///
    /// Denormalizes a time series.
    /// @method denormalize
    /// @memberof maianne
    /// @param {object}   data - The time series.
    /// @param {number}   minimum - The minimum value of the time series.
    /// @param {number}   maximum - The maximum value of the time series.
    /// @return {number}  Returns the denormalized time series.
    ///
    function denormalize(data, minimum, maximum) {
        denormalizedData = []

        for (i = 0; i < data.length; i = i + 1) {
            x = data[i, 0]
            if (core.type(x) == "number") {
                denormalizedX = x * maximum - x * minimum + minimum
                denormalizedData.push([denormalizedX])
            }
        }

        return(denormalizedData)
    }

    /// Starts the application and processes the data passed as command line arguments.
    /// @method run
    /// @memberof maianne
    function run() {
        // Supports only the Node.js interpreter.
        if (core.type(process) != "undefined") {
            command = "node"
            
            fs = system.require("fs")

            realPath = fs.realpathSync(system.argv[0])
            filePath = realPath.split("/")
            filePath = core.slice(filePath, 0, filePath.length - 2)
            filePath = filePath.join("/")
            this.scriptPath = filePath
            this.cnaLibrary = this.scriptPath + "/maia/cna/cna.maia"
            system.source(this.cnaLibrary)
            
            readTextFile = fs.readFileSync

            // Read file callback.
            function readFile(input) {
                content = readTextFile(input, "utf-8")
                return(content)
            }
            
            // Callback.
            function trainingCallback(epochs, RSS, correctness, ETL) {
                system.println(core.toString(epochs) + "," + core.toString(RSS) + "," + core.toString(correctness) + "," + core.toString(ETL))
            }
            
            // Command line options.
            inputFile = ""
            outputFile = ""
            reportFile = ""
            csvFile = ""
            correctnessFile = ""
            annFile = ""
            annOutputFile = ""
            itsFile = ""
            otsFile = ""
            linesToSkip = 0
            separator = ","
            lRate = 0.45
            af = "tanh"
            oaf = "linear"
            of = "none"
            ofc = [1, 0]
            allowLoops = false
            prepare = false
            negativeWeights = false
            maxEpochs = 1
            minimumCorrectness = 1.0
            interval = 0
            
            createANN = false
            topology = "mlp"
            vertices = 0
            edges = 0
            probability = 0
            avgdeg = 0
            ni = 0
            no = 0
            nl = 0
            nhu = 0
            prefix = "sample"
            nFiles = 1
            indexFrom = 1
            indexTo = 1
            indexIn = 1
            normalizeData = false
            denormalizeData = false
            minimum = ""
            maximum = ""

            // Get command line arguments.
            if (system.argc > 1) {
                i = 1
                while (i < system.argc) {
                    if ((system.argv[i] == "-h") | (system.argv[i] == "--help")) {
                        system.log(maianne.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maianne.appName + " [options] [input file] [--] [arguments]")
                        system.log("Options:")
                        system.log("-h     --help               Displays this help message.")
                        system.log("-i     [input file]         Input file name.")
                        system.log("-o     [output file]        Output file name.")
                        system.log("-r                          Statistics report file.")
                        system.log("-c                          Statistics CSV file.")
                        system.log("       --cf                 Correctness CSV file.")
                        system.log("       --ann                The artificial neural network file.")
                        system.log("       --annout             The artificial neural network output file.")
                        system.log("       --its                Input training set file.")
                        system.log("       --ots                Output training set file.")
                        system.log("       --skipfirst          Skip first line of input and output training set files.")
                        system.log("       --separator          Training set files column separator(default=,).")
                        system.log("       --lrate              Learning rate (default=0.45).")
                        system.log("       --af                 Activation function (default=logistic).")
                        system.log("       --oaf                Output layer activation function (default=logistic).")
                        system.log("       --of                 Output function(default=step).")
                        system.log("       --ofc                Output function coefficients.")
                        system.log("       --loops              Allow loops in network(biases).")
                        system.log("       --prepare            Prepare the artificil neural network to learning.")
                        system.log("       --nw                 Allow initial negative weights.")
                        system.log("       --epochs             Max number of epochs to learning.")
                        system.log("       --correct            Target correctness.")
                        system.log("       --interval           Epochs interval to show the learning progress.")
                        system.log("       --create             Create an artificial neural network.")
                        system.log("       --topology           Create an artificial neural network.")
                        system.log("       --vertices           Number of vertices.")
                        system.log("       --edges              Number of edges.")
                        system.log("       --probability        Edge probability.")
                        system.log("       --avgdeg             Average degree.")
                        system.log("       --ni                 Number of input neurons.")
                        system.log("       --no                 Number of output neurons.")
                        system.log("       --nl                 Number of layers")
                        system.log("       --nhu                Number of hidden units.")
                        system.log("       --prefix             Prefix for multiple neural network files (default=sample).")
                        system.log("       --nfiles             Number of files for multiple neural network files.")
                        system.log("       --from               First file index for multiple neural network files.")
                        system.log("       --to                 Last file index for multiple neural network files.")
                        system.log("       --in                 Total number of files in the file set.")
                        system.log("       --normalize          Normalizes a time series.")
                        system.log("       --denormalize        Denormalizes a time series.")
                        system.log("       --minimum            The minimum value of the time series.")
                        system.log("       --maximum            The maximum value of the time series.")
                        process.exit(0)
                    } elseif (system.argv[i] == "-i") {
                        i = i + 1
                        inputFile = system.argv[i]
                    } elseif (system.argv[i] == "-o") {
                        i = i + 1
                        outputFile = system.argv[i]
                    } elseif (system.argv[i] == "-r") {
                        i = i + 1
                        reportFile = system.argv[i]
                    } elseif (system.argv[i] == "-c") {
                        i = i + 1
                        csvFile = system.argv[i]
                    } elseif (system.argv[i] == "--cf") {
                        i = i + 1
                        correctnessFile = system.argv[i]
                    } elseif (system.argv[i] == "--ann") {
                        i = i + 1
                        annFile = system.argv[i]
                    } elseif (system.argv[i] == "--annout") {
                        i = i + 1
                        annOutputFile = system.argv[i]
                    } elseif (system.argv[i] == "--its") {
                        i = i + 1
                        itsFile = system.argv[i]
                    } elseif (system.argv[i] == "--ots") {
                        i = i + 1
                        otsFile = system.argv[i]
                    } elseif (system.argv[i] == "--skipfirst") {
                        linesToSkip = 1
                    } elseif (system.argv[i] == "--separator") {
                        i = i + 1
                        separator = system.argv[i]
                    } elseif (system.argv[i] == "--lrate") {
                        i = i + 1
                        lRate = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--af") {
                        i = i + 1
                        af = system.argv[i]
                    } elseif (system.argv[i] == "--oaf") {
                        i = i + 1
                        oaf = system.argv[i]
                    } elseif (system.argv[i] == "--of") {
                        i = i + 1
                        of = system.argv[i]
                    } elseif (system.argv[i] == "--ofc") {
                        i = i + 1
                        ofc = core.eval(system.argv[i])
                    } elseif (system.argv[i] == "--loops") {
                        allowLoops = true
                    } elseif (system.argv[i] == "--prepare") {
                        prepare = true
                    } elseif (system.argv[i] == "--nw") {
                        negativeWeights = true
                    } elseif (system.argv[i] == "--epochs") {
                        i = i + 1
                        maxEpochs = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--correct") {
                        i = i + 1
                        minimumCorrectness = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--interval") {
                        i = i + 1
                        interval = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--create") {
                        createANN = true
                    } elseif (system.argv[i] == "--topology") {
                        i = i + 1
                        topology = system.argv[i]
                    } elseif (system.argv[i] == "--vertices") {
                        i = i + 1
                        vertices = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--edges") {
                        i = i + 1
                        edges = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--probability") {
                        i = i + 1
                        probability = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--avgdeg") {
                        i = i + 1
                        avgdeg = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--ni") {
                        i = i + 1
                        ni = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--no") {
                        i = i + 1
                        no = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--nl") {
                        i = i + 1
                        nl = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--nhu") {
                        i = i + 1
                        nhu = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--prefix") {
                        i = i + 1
                        prefix = system.argv[i]
                    } elseif (system.argv[i] == "--nfiles") {
                        i = i + 1
                        nFiles = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--from") {
                        i = i + 1
                        indexFrom = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--to") {
                        i = i + 1
                        indexTo = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--in") {
                        i = i + 1
                        indexIn = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--normalize") {
                        normalizeData = true
                    } elseif (system.argv[i] == "--denormalize") {
                        denormalizeData = true
                    } elseif (system.argv[i] == "--minimum") {
                        i = i + 1
                        minimum = core.toNumber(system.argv[i])
                    } elseif (system.argv[i] == "--maximum") {
                        i = i + 1
                        maximum = core.toNumber(system.argv[i])
                    } else {
                        annFile = system.argv[i]
                        break
                    }
                    i = i + 1
                }
                if (createANN) {
                    // Calculates the file index number range.
                    if (indexFrom < 1) {
                        indexFrom = 1
                    }
                    if (indexTo <= 1) {
                        if (nFiles > 1) {
                            indexTo = indexFrom + nFiles - 1
                        }
                    }
                    nFiles = indexTo - indexFrom + 1
                    if (indexIn <= 1) {
                        indexIn = nFiles
                    }
                    for (nFile = indexFrom; nFile <= indexTo; nFile = nFile + 1) {
                        if (nFiles > 1) {
                            outputFile = prefix + "-" + string.sprintf("%0" + core.length(core.toString(indexIn)) + "." + core.length(core.toString(indexIn)) + "d", nFile) + ".net"
                        }

                        property = {
                            "adj": [],
                            "n": 0,
                            "m": 0,
                            "directed": false,
                            "density": 0,
                            "networkLabel": [],
                            "networkDegree": [],
                            "networkAverageDegree": 0,
                            "networkDegreeDistribution": [],
                            "networkDensity": 0,
                            "networkClustering": [],
                            "networkAverageClustering": 0,
                            "networkShortestPath": [],
                            "networkAverageShortestPath": 0,
                            "networkDiameter": 0,
                            "networkCentrality": [],
                            "networkVertexEfficiency": [],
                            "networkGlobalEfficiency": 0
                        }
                        
                        property.adj = ann.createANN(topology, vertices, edges, probability, avgdeg, ni, no, nl, nhu)
                        fileContents = cna.createPajekFile(property.adj, "arcs")

                        // Sets the output file.
                        if (outputFile == "") {
                            outputFile = "output.net"
                        }

                        fs.writeFileSync(outputFile, fileContents)
                    }
                } elseif (normalizeData) {
                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()

                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()
                    
                    if (outputFile == "") {
                        outputFile = fileName + "-normalized." + fileExtension
                    }

                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, true)
                    avgData = this.getAverage(inputData)
                    outputData = this.normalize(inputData, avgData.minimum, avgData.maximum)
                    
                    fileContents = system.createCSV(outputData, separator)
                    
                    fs.writeFileSync(outputFile, fileContents)

                    // Saves the report file.
                    reportFileContents = ""
                    reportFileContents = reportFileContents + "Average: " + core.toString(avgData.average) + "\n"
                    reportFileContents = reportFileContents + "Deviation: " + core.toString(avgData.deviation) + "\n"
                    reportFileContents = reportFileContents + "Minimum: "  + core.toString(avgData.minimum) + "\n"
                    reportFileContents = reportFileContents + "Maximum: "  + core.toString(avgData.maximum) + "\n"
                    reportFileContents = reportFileContents + "Events: "  + core.toString(avgData.n) + "\n"
                    
                    if (reportFile == "") {
                        reportFile = fileName + "-report.txt"
                    }

                    fs.writeFileSync(reportFile, reportFileContents)
                } elseif (denormalizeData) {
                    if ((core.type(minimum) != "number") || (core.type(maximum) != "number")) {
                        system.log("To denormalize a time series you need to provide the minimum and maximum values of the original, denormalized series.")
                        process.exit()
                    }

                    // Get the file without extension.
                    fileName = inputFile.split(".")
                    fileName = fileName.shift()

                    // Get the file name extension.
                    fileExtension = inputFile.split(".")
                    fileExtension = fileExtension.pop()
                    
                    if (outputFile == "") {
                        outputFile = fileName + "-denormalized." + fileExtension
                    }

                    inputData = system.loadCSV(inputFile, linesToSkip, separator, true, true)
                    outputData = this.denormalize(inputData, minimum, maximum)

                    fileContents = system.createCSV(outputData, separator)

                    fs.writeFileSync(outputFile, fileContents)
                } else {
                    if (annFile != "") {
                        local Glob = system.require("glob")
                        // Process each file based on glob pattern.
                        function processFiles(er, files) {
                            if (files.length == 0) {
                                system.log(maianne.appTitle + " Command Line Interface (CLI)")
                                system.log("Usage: " + maianne.appName + " [options] [input file] [--] [arguments]")
                            } else {
                                for (i = 0; i < files.length; i = i + 1) {
                                    // Get the full file name.
                                    file = files[i]
                                    
                                    // Get the file without extension.
                                    fileName = file.split(".")
                                    fileName = fileName.shift()

                                    // Get the file name extension.
                                    fileExtension = file.split(".")
                                    fileExtension = fileExtension.pop()

                                    // Read the file contents.
                                    fileContents = readFile(file)

                                    // Do something...
                                    property = {
                                        "adj": [],
                                        "n": 0,
                                        "m": 0,
                                        "directed": false,
                                        "networkLabel": [],
                                        "networkDegree": [],
                                        "networkAverageDegree": 0,
                                        "networkDegreeDistribution": [],
                                        "networkDensity": 0,
                                        "networkClustering": [],
                                        "networkAverageClustering": 0,
                                        "networkShortestPath": [],
                                        "networkAverageShortestPath": 0,
                                        "networkDiameter": 0,
                                        "networkCentrality": [],
                                        "networkVertexEfficiency": [],
                                        "networkGlobalEfficiency": 0
                                    }

                                    // Parses file contents.
                                    if (fileExtension == "json") {
                                        network = JSON.parse(fileContents)
                                        pajekFileContents = cna.jsonToPajekFile(network)
                                        property.adj = cna.parsePajekFile(pajekFileContents, property)
                                        property.networkLabel = cna.getLabels(property.adj)
                                    } elseif (fileExtension == "net") {
                                        property.adj = cna.parsePajekFile(fileContents, property)
                                        property.networkLabel = cna.getLabels(property.adj)
                                    } else {
                                        system.log("Unsupported file format when processing file " + file + "")
                                        process.exit()
                                    }
                                    
                                    if ((itsFile != "") && (otsFile != "")) {
                                        // Reads the train set.
                                        itsData = system.loadCSV(itsFile, linesToSkip, separator, true, true)
                                        otsData = system.loadCSV(otsFile, linesToSkip, separator, true, true)

                                        // Assigns random starting weights to neural synapses.
                                        if (prepare) {
                                            property.adj = ann.prepare(property.adj, true, allowLoops, negativeWeights)
                                        } else {
                                            property.adj = ann.prepare(property.adj, false, allowLoops, negativeWeights)
                                        }

                                        // Trains the neural network.
                                        startTime := Date()
                                        correctness = ann.training(property.adj, itsData, otsData, lRate, af, oaf, of, ofc, maxEpochs, minimumCorrectness, trainingCallback, 100)

                                        //correctness = ann.training(property.adj, itsData, otsData, lRate, af, oaf, of, ofc, maxEpochs, minimumCorrectness, trainingCallback, interval)
                                        endTime := Date()
                                        elapsedTime = endTime - startTime

                                        // Saves the trained network.
                                        trainedAnn = ann.reduce(property.adj)
                                        cna.setLabels(trainedAnn, property.networkLabel)
                                        
                                        trainedAnnFileContents = cna.createPajekFile(trainedAnn, "arcs")

                                        // Sets the output file.
                                        if (annOutputFile == "") {
                                            annOutputFile = fileName + "-trained.net"
                                        }

                                        fs.writeFileSync(annOutputFile, trainedAnnFileContents)

                                        // Saves correctness data.
                                        correctnessFileContents = system.createCSV(correctness[2], separator, ["rss", "correctness"])

                                        if (correctnessFile == "") {
                                            correctnessFile = fileName + "-correctness.net"
                                        }

                                        fs.writeFileSync(correctnessFile, correctnessFileContents)

                                        // Saves the report file.
                                        finalEpochs = core.toString(correctness[0])
                                        finalRss = core.toString(correctness[1])
                                        finalCorrectness = core.toString(correctness[2, core.length(correctness[2]) - 1, 1])

                                        reportFileContents = ""
                                        reportFileContents = reportFileContents + "Epochs: " + finalEpochs + "\n"
                                        reportFileContents = reportFileContents + "RSS: " + finalRss + "\n"
                                        reportFileContents = reportFileContents + "Correctness: "  + finalCorrectness + "\n"
                                        reportFileContents = reportFileContents + "Elapsed Time: "  + elapsedTime + "\n"
                                        
                                        if (reportFile == "") {
                                            reportFile = fileName + "-report.txt"
                                        }

                                        fs.writeFileSync(reportFile, reportFileContents)
                                    }

                                    // Uses the trained network to process the input data.
                                    if (inputFile != "") {
                                        inputData = system.loadCSV(inputFile, linesToSkip, separator, true, true)
                                        outputData = []

                                        for (indexData = 0; indexData < inputData.length; indexData = indexData + 1) {
                                            inData = inputData[indexData]
                                            outData = ann.think(property.adj, inData, ni, no, af, oaf, of, ofc)
                                            outputData.push(outData)
                                        }

                                        outputFileContents = system.createCSV(outputData, separator)

                                        // Sets the output file.
                                        if (outputFile == "") {
                                            outputFile = fileName + "-output.csv"
                                        }

                                        fs.writeFileSync(outputFile, outputFileContents)
                                    }
                                }
                            }
                        }

                        options = []

                        // Get all file names based on glob pattern.
                        local glob := Glob(annFile, options, processFiles)
                    } else {
                        system.log(maianne.appTitle + " Command Line Interface (CLI)")
                        system.log("Usage: " + maianne.appName + " [options] [input file] [--] [arguments]")
                    }
                }
            } else {
                system.log(maianne.appTitle + " Command Line Interface (CLI)")
                system.log("Usage: " + maianne.appName + " [options] [input file] [--] [arguments]")
            }
        }
    }
}

// Run the MaiaApp code if this script has been invoked
// from the command line.
if (core.type(process) != "undefined") {
    maianne.run()
}